
AC_HAVE_GL
KDE_HAVE_GL=yes;
if test "X$GLLIB" = "X"; then
    KDE_HAVE_GL=no
fi

AC_SUBST(KDE_HAVE_GL)   

AC_ARG_WITH(
   threshold,
   [  --with-threshold@<:@=0..15@:>@  Smoke: Qt tests threshold Default:10 Lower=more tests],
   [ qt_test_threshold="$withval" ],
   [ qt_test_threshold=10 ]
)
AC_SUBST(qt_test_threshold)

if test "X$kde_build_libsmoke" = "Xqt" -o "X$kde_build_libsmoke" = "Xqt kde"; then

    AC_CONFIG_FILES([ smoke/qt/qtguess.pl ], [
        cd smoke/qt
        perl qtguess.pl
        cd ../..
    ])
    AC_CONFIG_FILES([ smoke/qt/generate.pl ], [
    ])
fi

dnl -------
dnl Test for libqscintilla (QScintilla support)
dnl -------

AC_ARG_ENABLE(
   qscintilla,
   [  --enable-qscintilla@<:@=yes|no@:>@   Smoke: build Smoke with QScintilla extention for qt @<:@default:no@:>@],
   [ qtextscintilla="$withval" ],
   [ qtextscintilla="no" ]
)
AC_SUBST(qtextscintilla)

AC_DEFUN([AC_FIND_QSCINTILLA],
[
AC_MSG_CHECKING([for libqscintilla])
AC_CACHE_VAL(ac_cv_lib_qscintilla,
[
 kde_ldflags_save="$LDFLAGS"
 kde_libs_save="$LIBS"
 kde_cxxflags_save="$CXXFLAGS"

 LIBS="-lqscintilla $LIBQT $LIBS"
 CXXFLAGS="$CXXFLAGS $all_includes"
 LDFLAGS="$LDFLAGS $all_libraries $USER_LDFLAGS"

AC_TRY_LINK([
   #include <qextscintillabase.h>
],
[
            QextScintillaBase s;
],
            eval "ac_cv_lib_qscintilla='-lqscintilla'",
            eval "ac_cv_lib_qscintilla=no")

 LDFLAGS=$kde_ldflags_save
 LIBS=$kde_libs_save
 CXXFLAGS=$kde_cxxflags_save
])

if test ! "$ac_cv_lib_qscintilla" = no; then
  AC_DEFINE_UNQUOTED(HAVE_LIBQSCINTILLA, 1, [Define if you have libqscintilla])
  LIBQSCINTILLA="$ac_cv_lib_qscintilla"
  AC_MSG_RESULT($ac_cv_lib_qscintilla)
else
  AC_MSG_ERROR(not found. 
          Check your installation and look into config.log)
  LIBQSCINTILLA=""
fi
AC_SUBST(LIBQSCINTILLA)
])

if test "X$qtextscintilla" = "Xyes" ; then
  AC_FIND_QSCINTILLA
fi
